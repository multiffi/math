subprojects {

    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    group = 'multiffi'
    version = '1.0.0'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(Javadoc).configureEach { enabled = false }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = 'io.github.multiffi'
                artifactId = "multiffi-math-${project.name}"
                version = version
                from components.java
                pom {
                    name = 'io.github.multiffi'
                    description = project.description
                    url = 'https://github.com/multiffi/math'
                    licenses {
                        license {
                            name = 'BSD 3-Clause \"New\" or \"Revised\" License'
                            url = 'http://opensource.org/licenses/BSD-3-Clause'
                        }
                    }
                    developers {
                        developer {
                            id = 'Tianscar'
                            name = 'Naoko Mitsurugi'
                            email = 'tianscar@protonmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com/multiffi/math.git'
                        developerConnection = 'scm:git:git@github.com/multiffi/math.git'
                        url = 'https://github.com/multiffi/math'
                    }
                }
            }
        }
    }

    signing {
        sign(publishing.publications.mavenJava)
    }

}
